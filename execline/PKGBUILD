# Maintainer: Josh VanderLinden <arch@cloudlery.com>
pkgname=execline
pkgver=1.3.0
pkgrel=1
pkgdesc="A (non-interactive) scripting language, like sh"
arch=('i686' 'x86_64')
url="http://skarnet.org/software/execline/"
license=('ISC')
depends=('skalibs')
makedepends=()
source=("http://skarnet.org/software/execline/execline-${pkgver}.tar.gz")
md5sums=('6e100987c13014055c2c07430a0a16b5')

build() {
  cd "$srcdir/admin/$pkgname-$pkgver"

  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/libexec/execline    > conf-compile/conf-install-command
  echo /usr/lib/execline        > conf-compile/conf-install-library
  echo /usr/include/execline    > conf-compile/conf-install-include
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library

  rm -f conf-compile/flag-slashpackage
  #rm -f conf-compile/flag-allstatic

  package/compile
}

package() {
  cd "$srcdir/admin/$pkgname-$pkgver"
  for i in package/*.exported
  do
    case `basename $i` in
      library.so.exported) d=lib                    ;;
      include.exported)    d=usr/include/execline   ;;
      library.exported)    d=usr/lib/execline       ;;
      command.exported)    d=usr/libexec/execline   ;;
    esac
    f=`basename $i|sed 's/.exported//'`
    mkdir -p $pkgdir/$d
    install `sed s,^,$f/, $i` "$pkgdir/$d"
  done

  #install -D -m755 "$srcdir/execlineb.eb" "$pkgdir/usr/bin/execlineb"
  #install -D -m755 "$srcdir/execline.eb"  "$pkgdir/usr/bin/execline"
  install -D -m644 package/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:ft=sh ts=2 sw=2 et:
