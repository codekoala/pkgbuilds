# Maintainer: xeross <contact@xeross.me>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=etcd
pkgver=0.3.0
pkgrel=3
pkgdesc="A highly-available key value store for shared configuration and service discovery"
arch=('x86_64' 'i686')
url="http://coreos.com/docs/etcd/"
license=('Apache')
depends=()
makedepends=('git' 'go')
source=(
   "${pkgname}::git://github.com/coreos/etcd.git#tag=v${pkgver}"
   "${pkgname}ctl::git://github.com/coreos/etcdctl.git#tag=v${pkgver}"
   "./etcd.service"
   "./etcd.conf"
)
install=etcd.install
backup=('etc/conf.d/etcd')
sha256sums=('SKIP'
            'SKIP'
            'dd0e76e58da1fe99c4ad3739f06e70fb0acfad8d4725d581b7e76acd57e8fef8'
            '514e6ab46330a376499e78f45eb65897ac44cf87c5ae62269f159939a74662ac')

build() {
  cd "$srcdir/$pkgname/"
  ./build

  cd "$srcdir/etcdctl/"
  ./build
}

package() {
  # install configuration file
  install -Dm644 "${srcdir}/etcd.conf" "${pkgdir}/etc/conf.d/etcd"

  # make data directory
  install -dm755 "${pkgdir}/var/lib/etcd"

  # install binaries
  install -D -m 755 "${srcdir}/etcd/bin/etcd" "$pkgdir/usr/bin/etcd"
  install -D -m 755 "${srcdir}/etcdctl/bin/etcdctl" "$pkgdir/usr/bin/etcdctl"

  # install service
  install -Dm644 "${srcdir}/etcd.service" "${pkgdir}/usr/lib/systemd/system/etcd.service"
}

# vim:set ts=2 sw=2 et:
