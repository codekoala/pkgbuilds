# Maintainer: philomath <philomath868 AT gmail DOT com>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
pkgname=skalibs
pkgver=1.4.2
pkgrel=3
pkgdesc="A general-purpose utility library for secure, small C development"
url="http://www.skarnet.org/software/${pkgname}/"
license=('ISC')
arch=('i686' 'x86_64')
source=(http://www.skarnet.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('e2bfa4447977024e1f2f91e9eb880baa')

build() {
  cd ${srcdir}/prog/${pkgname}-${pkgver}
  package/compile
}

package() {
  cd ${srcdir}/prog/${pkgname}-${pkgver}

  for i in package/*.exported
  do
    case $(basename ${i} ) in
      library.so.exported) d=lib                        ;;
      include.exported)    d=usr/include/${pkgname}     ;;
      library.exported)    d=usr/lib/${pkgname}         ;;
      sysdeps.exported)    d=usr/lib/${pkgname}/sysdeps ;;
      command.exported)    d=usr/libexec/${pkgname}     ;;
    esac
    f=$(basename ${i} | sed 's/.exported//')

    mkdir -p ${pkgdir}/${d}
    install $(sed s,^,${f}/, ${i}) "${pkgdir}/${d}"
  done

  install -Dm644 doc/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
