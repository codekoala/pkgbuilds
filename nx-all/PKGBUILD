# Maintainer: Josh VanderLinden <arch@cloudlery.com>
pkgname=nx-all
pkgver=3.5.0
pkgrel=7
pkgdesc="NoMachine nxclient, nxnode, and nxserver in one package"
arch=('i686' 'x86_64')
url="https://bitbucket.org/instarch/pkgbuilds/src/master/nx-all"
license=('custom:nomachine')
depends=('psmisc' 'tigervnc')
provides=('nxclient' 'nxnode' 'nxserver')
conflicts=('freenx' 'nxclient')
install=nx-all.install
source=(
  "LICENSE"
  "nxsensor.service"
  "nxserver.service"
  "nx-all.install"
  "nxnode-arch.patch"
  "nxserver-arch.patch"
)
md5sums=('521a98ac0d4b2c88887acf98b3182aff'
         '246426c32a2a05c7d8e5cb7a30c03acc'
         'b9f6d1c446b51a3f6f652980e2edf670'
         '3df0ec49cf305b5fc9f1ce9ea80fd0cd'
         'cdebef58a20442e825a654a7cb2f0f5e'
         'e33d73b40c1d8467bbf0c273e2026cc8')

if [ "${CARCH}" = "i686" ]; then
  source+=(
    "http://instarch.codekoala.com/nxclient-3.5.0-7.i386.tar.gz"
    "http://instarch.codekoala.com/nxnode-3.5.0-9.i386.tar.gz"
    "http://instarch.codekoala.com/nxserver-3.5.0-11.i386.tar.gz"
  )
  md5sums+=('0bc9f652cb294bc3eabc71e6ca6fe5dd'
            '584d5a9131c72fa7f4e71dd2042b6044'
            '8133fa3ff34d10a1327181b2f98a56bc')
else
  source+=(
    "http://instarch.codekoala.com/nxclient-3.5.0-7.x86_64.tar.gz"
    "http://instarch.codekoala.com/nxnode-3.5.0-9.x86_64.tar.gz"
    "http://instarch.codekoala.com/nxserver-3.5.0-11.x86_64.tar.gz"
  )
  md5sums+=('3d3ba27c7bf78f206964237892590140'
            '14f9b86a0a866bf9535414c25fa2f299'
            '523a962b69c433791c70aeb3a50bad08')
fi

package() {
  install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D nxsensor.service "${pkgdir}/usr/lib/systemd/system/nxsensor.service"
  install -D nxserver.service "${pkgdir}/usr/lib/systemd/system/nxserver.service"

  msg "Creating configuration files for Arch"
  local nodecfg="${srcdir}/NX/etc/node-arch.cfg.sample"
  install -D "${srcdir}/NX/etc/node-debian.cfg.sample" "${nodecfg}"
  sed -i 's,^CommandFuser =.*$,CommandFuser = "/usr/bin/fuser",' "${nodecfg}"

  local srvcfg="${srcdir}/NX/etc/server-arch.cfg.sample"
  install -D "${srcdir}/NX/etc/server-debian.cfg.sample" "${srvcfg}"
  sed -i 's/^#SSHAuthorizedKeys = .*$/SSHAuthorizedKeys = "authorized_keys"/' "${srvcfg}"

  msg "Patching setup scripts for Arch"
  patch -Np2 -i nxnode-arch.patch
  patch -Np2 -i nxserver-arch.patch

  cp -a "${srcdir}/NX" "${pkgdir}/usr/"
}

# vim:set ts=2 sw=2 et:
