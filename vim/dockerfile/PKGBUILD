# Maintainer: Josh VanderLinden <arch@cloudlery.com>
pkgname=vim-dockerfile-git
pkgver=r29.6298630
pkgrel=1
pkgdesc="Vim syntax file & snippets for Docker's Dockerfile"
arch=('any')
url="https://github.com/ekalinin/Dockerfile.vim"
license=('MIT')
depends=('vim')
makedepends=('git')
source=("${pkgname}"::"git+https://github.com/ekalinin/Dockerfile.vim")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${pkgname}"
  install -Dm644 ./syntax/Dockerfile.vim ${pkgdir}/usr/share/vim/vimfiles/syntax/Dockerfile.vim
  install -Dm644 ./ftdetect/Dockerfile.vim ${pkgdir}/usr/share/vim/vimfiles/ftdetect/Dockerfile.vim
  install -Dm644 ./snippets/Dockerfile.snippets ${pkgdir}/usr/share/vim/vimfiles/snippets/Dockerfile.snippets

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
