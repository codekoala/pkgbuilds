# Maintainer: Josh VanderLinden <arch@cloudlery.com>
pkgname=s6
pkgver=1.1.3
pkgrel=1
pkgdesc="A small suite of programs for UNIX, designed to allow process supervision"
arch=('i686' 'x86_64')
url="http://skarnet.org/software/${pkgname}/"
license=('ISC')
depends=('skalibs' 'execline')
source=("http://skarnet.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('f4087d25449e1da4f6021d53ac778ae5')

build() {
  cd "${srcdir}/admin/${pkgname}-${pkgver}"

  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/bin/                > conf-compile/conf-install-command
  echo /usr/lib/                > conf-compile/conf-install-library
  echo /usr/include/            > conf-compile/conf-install-include
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/include/execline    >>conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library
  echo /usr/lib/execline        >>conf-compile/path-library

  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic

  package/compile
}

package() {
  cd "${srcdir}/admin/${pkgname}-${pkgver}"

  for i in package/*.exported
  do
    case $(basename ${i} ) in
      library.so.exported) d=lib         ;;
      include.exported)    d=usr/include ;;
      library.exported)    d=usr/lib     ;;
      command.exported)    d=usr/bin     ;;
    esac
    f=$(basename ${i} | sed 's/.exported//')

    mkdir -p ${pkgdir}/${d}
    install $(sed s,^,${f}/, ${i}) "${pkgdir}/${d}"
  done

  install -D -m644 package/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:ft=sh ts=2 sw=2 et:
